%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Tools to manipulate percepts from Agents in City Game
%
% Author: Sebastian Sardina 2017-2018 (ssardina@gmail.com) 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
:- dynamic 
	id/1, 			% id
	percepts/3,		% main percept data
	percepts_nonself/2, 	% Stores percepts of others
	percepts_self/3, 	% Stores percepts about myself
	step/1, 		% Stores current simulator step number
	agentName/1,		% Stores the name of the agent who owns the KB
	player/1.		% Stores names of connection players controlled

:- dynamic storage/6, 
	dump/3,
	chargingStation/4,
	workshop/3,
	resourceNode/4.

%% Here is an example of a percepts/3 predicate:
%%
%percepts(example,0,[item(item4,41,tools([]),parts([])), simStart, item(item2,44,tools([]),parts([])), maxLat(19.45), item(item0,21,tools([]),parts([])), item(tool2,33,tools([]),parts([])), team(A), item(tool6,38,tools([]),parts([])), item(item14,354,tools([tool4,tool2]),parts([[item3,3],[item13,1],[item9,1]])), role(drone,5,100,250,[tool4,tool6,tool7]), item(tool0,35,tools([]),parts([])), minLat(19.3), item(item12,64,tools([tool7,tool5]),parts([[item7,1],[item11,1]])), item(item8,94,tools([tool1,tool5]),parts([[item7,3],[item0,1],[item4,1]])), item(item9,126,tools([tool1]),parts([[item2,3],[item6,1]])), item(tool4,47,tools([]),parts([])), item(item5,42,tools([]),parts([])), item(item6,15,tools([]),parts([])), item(item1,43,tools([]),parts([])), item(tool5,34,tools([]),parts([])), item(tool1,28,tools([]),parts([])), id(2017-Mexico-City-Test), steps(1000), map(mexico-city), item(item11,60,tools([tool3]),parts([[item7,2],[item6,1]])), item(tool3,37,tools([]),parts([])), item(item10,166,tools([]),parts([[item0,3],[item4,3],[item6,1]])), item(item7,30,tools([]),parts([])), item(tool7,48,tools([]),parts([])), seedCapital(50000), minLon(-99.2), maxLon(-99.0), item(item13,270,tools([tool1]),parts([[item4,1],[item8,3],[item2,1]])), item(item3,36,tools([]),parts([])), storage(storage13,19.44019,-99.18794,14889,0,[]), storage(storage7,19.35245,-99.04573,13230,0,[]), storage(storage14,19.42892,-99.10555,8183,0,[]), route([]), shop(shop4,19.35746,-99.19067,5,[item(item0,74,20),item(item1,84,11),item(item2,84,7),item(item5,63,19),item(item6,98,17),item(item7,19,14)]), dump(dump8,19.39958,-99.13238), chargingStation(chargingStation15,19.42892,-99.11276,107), workshop(workshop6,19.40399,-99.18714), shop(shop10,19.39051,-99.03542,4,[item(item1,86,7),item(item2,81,12),item(item3,122,12),item(item5,69,6),item(item7,14,16),item(tool2,45,16),item(tool4,47,11),item(tool6,124,17),item(tool7,48,8)]), entity(PlayerA5,A,19.40736,-99.05846,motorcycle), workshop(workshop5,19.35316,-99.03779), dump(dump9,19.41991,-99.07416), entity(PlayerA7,A,19.33828,-99.12541,motorcycle), shop(shop1,19.32016,-99.12328,3,[item(item1,91,18),item(tool0,93,10),item(tool1,72,16),item(tool5,107,9),item(tool6,138,9),item(tool7,39,6)]), lastActionResult(successful), chargingStation(chargingStation1,19.30823,-99.14639,123), entity(PlayerA19,A,19.37567,-99.11684,car), chargingStation(chargingStation3,19.30137,-99.04509,91), workshop(workshop7,19.39895,-99.0919), entity(PlayerA25,A,19.36242,-99.02579,truck), chargingStation(chargingStation12,19.3876,-99.07765,128), storage(storage2,19.32609,-99.09198,12589,0,[]), dump(dump13,19.4271,-99.03601), shop(shop0,19.33569,-99.16989,1,[item(item4,101,13),item(tool0,84,20),item(tool3,126,18),item(tool5,108,16),item(tool6,101,12)]), workshop(workshop9,19.44614,-99.19569), chargingStation(chargingStation4,19.30961,-99.03329,119), storage(storage12,19.39664,-99.03379,9594,0,[]), workshop(workshop10,19.44087,-99.14081), step(0), entity(PlayerA4,A,19.43752,-99.08263,drone), entity(PlayerA27,A,19.33246,-99.10792,truck), lastActionParams([]), chargingStation(chargingStation5,19.35958,-99.1865,73), dump(dump7,19.36426,-99.03392), storage(storage0,19.32686,-99.19329,9604,0,[]), entity(PlayerA24,A,19.37424,-99.05352,truck), entity(PlayerA17,A,19.37227,-99.13973,car), dump(dump0,19.33927,-99.18741), chargingStation(chargingStation2,19.33813,-99.08956,98), facility(), entity(PlayerA21,A,19.3971,-99.05624,truck), chargingStation(chargingStation9,19.35086,-99.0163,53), lastAction(noAction), entity(PlayerA18,A,19.39969,-99.02349,car), shop(shop2,19.32144,-99.0697,3,[item(item0,90,14),item(item6,91,10),item(tool5,133,19)]), workshop(workshop3,19.37344,-99.09335), shop(shop13,19.44068,-99.1147,5,[item(item5,69,6),item(tool0,69,19),item(tool1,85,20)]), storage(storage6,19.3626,-99.11757,13702,0,[]), chargingStation(chargingStation0,19.31722,-99.18127,140), dump(dump10,19.38139,-99.02397), entity(PlayerA12,A,19.33519,-99.09793,motorcycle), entity(PlayerA20,A,19.40171,-99.19236,car), storage(storage1,19.30076,-99.12679,7824,0,[]), entity(PlayerA3,A,19.40928,-99.11384,drone), dump(dump5,19.34982,-99.14985), shop(shop9,19.39585,-99.04156,1,[item(item0,70,18),item(item3,95,9),item(item4,141,20),item(item7,16,6),item(tool4,37,20),item(tool5,121,17),item(tool6,120,8),item(tool7,45,9)]), chargingStation(chargingStation7,19.37113,-99.11836,54), storage(storage5,19.36626,-99.12744,11163,0,[]), chargingStation(chargingStation8,19.3591,-99.05357,111), entity(PlayerA1,A,19.37562,-99.00317,drone), chargingStation(chargingStation11,19.41131,-99.14265,130), storage(storage8,19.3902,-99.18367,12169,0,[]), lon(-99.17834), shop(shop14,19.42382,-99.03512,3,[item(item1,99,17),item(item3,105,9),item(item4,139,19),item(item7,14,20),item(tool0,72,13),item(tool4,36,15),item(tool6,97,19)]), entity(PlayerA13,A,19.43401,-99.06124,car), chargingStation(chargingStation10,19.38384,-99.17284,122), dump(dump11,19.42418,-99.17356), workshop(workshop11,19.43077,-99.02892), storage(storage11,19.41385,-99.05545,13467,0,[]), entity(PlayerA23,A,19.44255,-99.15922,truck), shop(shop12,19.43897,-99.14012,2,[item(item1,82,10),item(tool2,56,15),item(tool3,118,19)]), entity(PlayerA9,A,19.32065,-99.13607,motorcycle), entity(PlayerA26,A,19.38986,-99.14356,truck), storage(storage10,19.40745,-99.10503,11491,0,[]), entity(PlayerA10,A,19.3473,-99.19299,motorcycle), actionID(0), requestAction, timestamp(1514759013919), storage(storage3,19.30459,-99.01727,14934,0,[]), shop(shop7,19.39279,-99.1337,5,[item(item0,67,17),item(item1,92,13),item(item2,87,5),item(tool0,68,11),item(tool7,47,17)]), entity(PlayerA14,A,19.44007,-99.14925,car), entity(PlayerA11,A,19.44882,-99.19551,motorcycle), entity(PlayerA22,A,19.33237,-99.02805,truck), dump(dump2,19.31402,-99.05739), dump(dump12,19.44191,-99.08681), shop(shop8,19.38367,-99.10324,3,[item(item2,84,7),item(item4,144,16),item(item7,17,8),item(tool1,82,11),item(tool3,141,12),item(tool4,39,20),item(tool5,133,9),item(tool6,132,13),item(tool7,53,19)]), entity(PlayerA8,A,19.30023,-99.19738,motorcycle), dump(dump6,19.37424,-99.09267), deadline(1514759017919), workshop(workshop8,19.41512,-99.01923), charge(250), shop(shop3,19.33139,-99.00328,1,[item(item0,70,20),item(tool0,91,9),item(tool7,42,10)]), dump(dump3,19.31904,-99.01868), workshop(workshop2,19.33312,-99.08554), storage(storage9,19.41272,-99.15535,9557,0,[]), storage(storage4,19.36119,-99.17035,8040,0,[]), workshop(workshop4,19.37509,-99.05729), lat(19.38689), shop(shop6,19.39694,-99.1675,5,[item(item1,79,15),item(item2,62,14),item(item5,78,10),item(item7,16,17),item(tool4,39,18)]), entity(PlayerA2,A,19.38689,-99.17834,drone), shop(shop5,19.37946,-99.07389,2,[item(item1,103,15),item(item4,99,9),item(item7,17,13),item(tool1,89,11),item(tool4,37,17),item(tool5,103,7),item(tool6,129,11)]), chargingStation(chargingStation13,19.38638,-99.02568,79), entity(PlayerA16,A,19.31248,-99.0403,car), entity(PlayerA6,A,19.30774,-99.01944,motorcycle), money(50000), entity(PlayerA15,A,19.30406,-99.10986,car), chargingStation(chargingStation16,19.4328,-99.03119,89), storage(storage15,19.43708,-99.0397,14486,0,[]), routeLength(0), entity(PlayerA28,A,19.30451,-99.04253,truck), dump(dump1,19.3315,-99.10955), workshop(workshop1,19.33872,-99.15421), workshop(workshop0,19.33281,-99.1987), dump(dump4,19.34984,-99.19905), shop(shop11,19.42324,-99.19889,4,[item(item1,98,8),item(item3,101,12),item(item4,104,9),item(item6,97,14),item(tool1,87,15)]), load(0), chargingStation(chargingStation6,19.35604,-99.12279,120), chargingStation(chargingStation14,19.447,-99.1726,51)]).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Defines the TYPE of each single percept that may come in as json data
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
percept_simStart_type(simStart).
percept_simStart_type(id(_)).		% problem with arguments in capital treated as vars
percept_simStart_type(map(_)).		% problem with arguments in capital treated as vars
percept_simStart_type(seedCapital(_)).
percept_simStart_type(steps(_)).
percept_simStart_type(team(_)).		% problem with arguments in capital treated as vars
percept_simStart_type(role(_, _, _, _, _)).
percept_simStart_type(item(_, _, _, _)).
percept_simStart_type(proximity(_)).
percept_simStart_type(cellSize(_)).
percept_simStart_type(maxLat(_)).
percept_simStart_type(minLat(_)).
percept_simStart_type(centerLat(_)).
percept_simStart_type(maxLon(_)).
percept_simStart_type(minLon(_)).
percept_simStart_type(centerLon(_)).


percept_simEnd_type(simEnd).
percept_simEnd_type(ranking(_)).
percept_simEnd_type(score(_)).

percept_self_type(name(_)). 
percept_self_type(actionID(_)).
percept_self_type(timestamp(_)).
percept_self_type(deadline(_)).
%percept_self_type(step(_)).
percept_self_type(charge(_)).
percept_self_type(load(_)).
percept_self_type(lat(_)).
percept_self_type(lon(_)).
percept_self_type(routeLength(_)).
%percept_self_type(money(_)).
percept_self_type(facility(_)).
percept_self_type(lastAction(_)).
percept_self_type(lastActionParams(_)).
percept_self_type(lastActionResult(_)).
percept_self_type(hasItem(_, _)).
percept_self_type(route(_)).


percept_nonself_type(workshop(_, _, _)).
percept_nonself_type(dump(_, _, _)).
percept_nonself_type(storage(_, _, _, _, _, _)).
percept_nonself_type(item(_, _, _, _)).
percept_nonself_type(money(_)).
percept_nonself_type(entity(_, _, _, _, _)).
percept_nonself_type(chargingStation(_, _, _, _)).
percept_nonself_type(step(_)).
percept_nonself_type(resourceNode(_, _, _, _)).
percept_nonself_type(job(_, _, _, _, _)).
percept_nonself_type(job(_, _, _, _, _, _)).
percept_nonself_type(posted(_, _, _, _, _)).
percept_nonself_type(posted(_, _, _, _, _, _)).
percept_nonself_type(auction(_, _, _, _, _, _, _, _)).
percept_nonself_type(auction(_, _, _, _, _, _, _, _, _)).
percept_nonself_type(mission(_, _, _, _, _, _, _, _)).
percept_nonself_type(mission(_, _, _, _, _, _, _, _, _)).


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% USEFUL abtractions
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
facility(F, storage) :- storage(F, _, _, _, _, _).
facility(F, dump) :- dump(F, _, _).
facility(F, chargingStation) :- chargingStation(F, _, _, _).
facility(F, workshop) :- workshop(F, _, _).
facility(F, resourceNode) :- resourceNode(F, _, _, _).


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% percepts(Player, Step, Percepts): 
%	this is asserted when the Player receives the list of Percepts at step Step
%	this is the most low-level information recorded; Percepts has everything sensed!
%
% By processing this predicate, we can keep more focused predicates:
%
% percepts_self(Player, Step, Percepts)
%	this stores all the Percepts at Step that apply only to Player 
% percepts_nonself(Step, NonSelfPercepts): 
%	this is the information that applies to everyone
% percepts_simStart(SimStartPercepts)
%	this is the information in the sim-start message
% percepts_simEnd(SimEndPercepts)
%	this is the information in the sim-end message
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Several get operations from percepts
% This assumes percepts/3 have been recorded
% Will yield info for all steps recorded
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
get_player_data(Player, Name, Step, Lat, Long, Charge, Load, Money, Facility) :- 
	percepts(Player, Step, Percepts),
	member(name(Name), Percepts),
	member(lat(Lat), Percepts),
	member(lon(Long), Percepts),
	member(load(Load), Percepts),
	member(charge(Charge), Percepts),
	member(money(Money), Percepts),
	(member(facility(Facility), Percepts) - true ; Facility = none).
get_player_name(Player, Name) :-
	get_last_step(Player, Step),  
	percepts(Player, Step, Percepts), 
	member(name(Name), Percepts).
get_player_loc(Player, Step, Lat, Long) :- 
	percepts(Player, Step, Percepts),
	member(lat(Lat), Percepts),
	member(lon(Long), Percepts).
get_player_charge(Player, Step, Charge) :- 
	percepts(Player, Step, Percepts),
	member(charge(Charge), Percepts).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Get operations from the last percept recorded
% Using once(G) will yield just the first success of G
% See once/1 doc: http://www.swi-prolog.org/pldoc/man?predicate=once/1
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
get_last_self_percepts(Player, Step, Percepts) :- 
	player(Player),
	once(percepts_self(Player, Step, Percepts)).
get_last_step(Player, Step) :-
	player(Player),
	once(percepts(Player, Step, _)).
get_player_last_loc(Player, Step, Lat, Long) :- 
	player(Player),
	once(get_player_loc(Player, Step, Lat, Long)).
	

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Extract each percept type from a general percept
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
extract_simStart_percepts(Percepts, SimStartPercepts) :-
	findall(Percept, (member(Percept, Percepts), percept_simStart_type(Percept)), SimStartPercepts).
extract_simEnd_percepts(Percepts, SimEndPercepts) :-
	findall(Percept, (member(Percept, Percepts), percept_simEnd_type(Percept)), SimEndPercepts).
extract_requestAction_percepts(Percepts, ReqActionPercepts) :-
	findall(Percept, (member(Percept, Percepts), \+ percept_simEnd_type(Percept), \+ percept_simStart_type(Percept)), ReqActionPercepts).
extract_self_percepts(Percepts, SelfPercepts) :-
	findall(Percept, (member(Percept, Percepts), percept_self_type(Percept)), SelfPercepts).
extract_nonself_percepts(Percepts, NonSelfPercepts) :-
	findall(Percept, (member(Percept, Percepts), percept_nonself_type(Percept)), NonSelfPercepts).


assert_percepts(Player, Step, Percepts) :- 
	foreach(member(Percept, Percepts), asserta(percept_term(Player, Step, Percept))).

 	
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Process the last percept stored for Player by extracting and asserting each of its 4 component types:
%
% percepts_simStart(SimStartPercepts)
% percepts_self(Player, Step, SelfPercepts): data for player itself
% percepts_nonself(Step, NonSelfPercepts): data that applies for everyone
% percepts_simEnd(SimEndPercepts)
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
process_last_percepts(Player) :-
	percepts(Player, _, Percepts), !, % get the last percepts/3 asserted in the DB
	process_percepts(Player, Percepts).
	
% Transform list of percepts into different local predicates
process_percepts(Player, Percepts) :-		
	process_simStart_percepts(Percepts),		% produces percepts_simStart/1 and one per data
	process_self_percepts(Player, Percepts),		% produces percepts_self/3
	process_nonself_percepts(Percepts),		% produces percepts_nonself/2
	process_simEnd_percepts(Percepts).		% produces percepts_simEnd/1 and one per data


process_simStart_percepts(Percepts) :-	% simstart percepts
	\+ id(_), !, 			% not processed before (this is just 1 time message)
	member(simStart, Percepts),
	extract_simStart_percepts(Percepts, SimStartPercepts),
	foreach(member(Percept, SimStartPercepts), asserta(Percept)),
	asserta(percepts_simStart(SimStartPercepts)).
process_simStart_percepts(_).

process_self_percepts(Player, Percepts) :-	% self percepts from request action
	member(step(Step), Percepts), 
	\+ percepts_self(Player, Step, _), !,
	extract_self_percepts(Percepts, SelfPercepts),
	asserta(percepts_self(Player, Step, SelfPercepts)).
process_self_percepts(_, _).

process_nonself_percepts(Percepts) :-	% non-self percepts from request action
	member(step(Step), Percepts),
	\+ percepts_nonself(Step, _), !,
	extract_nonself_percepts(Percepts, NonSelfPercepts),
	foreach(percept_nonself_type(X), retractall(X)),
	foreach(member(Percept, NonSelfPercepts), asserta(Percept)),
	asserta(percepts_nonself(Step, NonSelfPercepts)).
process_nonself_percepts(_).

process_simEnd_percepts(Percepts) :-	% simend percepts
	member(simEnd, Percepts), !,
	extract_simEnd_percepts(Percepts, SimEndPercepts),
	foreach(member(Percept, SimEndPercepts), asserta(Percept)),
	asserta(percepts_simEnd(SimEndPercepts)).
process_simEnd_percepts( _).

	

% Clean up all stored information about percepts
clean_percepts :-
	(percept_self_type(T) ; percept_simStart_type(T) ; percept_simEnd_type(T)), 
	retractall(T),
	fail.
clean_percepts :-
	retractall(percepts_simStart(_)),
	retractall(percepts_self(_, _, _)),
	retractall(percepts_nonself(_, _)),	
	retractall(percepts_simEnd(_)).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% EOF
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%	
