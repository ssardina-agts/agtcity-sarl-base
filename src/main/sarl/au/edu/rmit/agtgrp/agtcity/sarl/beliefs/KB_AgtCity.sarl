/** 
 * SARL-AGTCITY-BASE - Team controller for Agents in City Game
 * Copyright (C) 2018 Sebastian Sardina.

 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.

 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.

 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
package au.edu.rmit.agtgrp.agtcity.sarl.beliefs

import eis.iilang.Percept
import io.sarl.core.Logging
import io.sarl.extras.SWIJPL_KB_Prolog
import java.util.ArrayList
import java.util.Collection

/** 
 * @author Sebastian Sardina (ssardina@gmail.com)
 * 
 */
capacity KB_AgtCity {
	def kb_load(file : String)
	def kb_dump()
	def kb_recordKBName(name : String)
	def kb_recordPercepts(entityName : String, stepNo : int, percepts : String)
	def kb_recordPercepts(entityName : String, stepNo : int, percepts : Collection<Percept>)
	def kb_processLastPercept(entityName : String)
	def kb_pickDestination(entityName : String) : String

	def kb_setLogLevel(logLevel : int)
}

/**
 * Skill for KB_AgtCity using SWI Prolog
 */
@SuppressWarnings("potential_field_synchronization_problem")
skill SWI_KB_AgtCity implements KB_AgtCity extends SWIJPL_KB_Prolog {
	uses Logging

	new (name : String, logLevel : int = 0, kb_dump_dir : String) {
		super(name, kb_dump_dir) // Call the super's constructor
		kb_setLogLevel(logLevel)
	}

	new (name : String, logLevel : int = 0) {
		super(name) // Call the super's constructor
		kb_setLogLevel(logLevel)
	}

	// For compatibility for old version
	@Deprecated
	new (logLevel : int = 0, name : String) {
		this(name, logLevel)
	}

	def kb_setLogLevel(logLevel : int) {
		 
	}

	
	def kb_load(file : String) {
		consult(file)
	}

	def kb_dump() {
		dump_kb()
	}
	

	def kb_recordKBName(name : String) {
		assertFirst("entity_name(?)", name)
	}

	def kb_recordPercepts(entityName : String, stepNo : int, percepts : String) {
		assertFirst("percepts(?, ?, ?)", entityName, stepNo, percepts)
		
	}

	def kb_recordPercepts(entityName : String, stepNo : int, percepts : Collection<Percept>) {
		val perceptsProlog : Collection<String> = new ArrayList<String>

		percepts.forEach[percept : Percept|perceptsProlog.add(percept.toProlog())]
		
		assertFirst("percepts(?, ?, ?)", entityName, stepNo, perceptsProlog.toString)
	}

	def kb_processLastPercept(entityName : String) {
		prove("process_last_percepts(?)", entityName)
	}

	def kb_pickDestination(entityName : String) : String {
		val solution = askOnce(String.format("choose_destination('%s', Destination)", entityName))
//		solution = askOnce("choose_destination(@S, Destination)", entityName)	// Using Mochalog query builder formatter
		
		if (solution !== null) return solution.get("Destination").name else return null
	}

	// Function invoked when the skill is installed in the owner.
	// You should put all the initialization statements in this block of code.
	def install {
		super.install	// Do the install of the higher class first.
		debug("Domain KB_AgtCity skill INSTALLED")
	}

	def uninstall {
		// Function invoked when the skill is uninstalled from the owner.
		// You should put all the resource release statements in this block of code.
		debug("Domain KB_AgtCity skill UNINSTALLED")
	}

}
